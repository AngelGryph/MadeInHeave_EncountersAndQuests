DEFINE_ACTION_FUNCTION tsolak_battle
BEGIN
  ACTION_IF NOT FILE_EXISTS_IN_GAME "vampreg2.itm"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_item
      STR_VAR
      item		= "vampreg=>vampreg1"
      editstring	= ~match=>"opcode=98" parameter1=>1~
    END
  END

  MAKE_PATCH
    strength=>18
    strength_ex=>100
    constitution=>18
    dexterity=>18
    intelligence=>18
    charisma=>17
    xp_value=>12500
    hitpoints=>89
    ac=>"-1"
    level1=>11
    enforce_thac0=>at_worst
    enforce_saves=>at_worst
    attacks=>2
    resist_fire=>50
    resist_cold=>50
    resist_electricity=>50
    resist_acid=>50
    resist_magic=>20
    add_effect_inline=>"opcode=>31 target=>1 timing=>9 parameter1=>50 parameter2=>1"
    replace_items=>"vampreg1(lring) immune2(rring)"
  END   

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature		= "bdtsolak"
    edits		= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature		= "bdtsolak=>mh#ts2nd"
    editstring		= ~script_override=>"mh#ts2nd" dialog=>"none" allegiance=>"enemy"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature		= "bdisabel"
    editstring		= ~replace_items=>"sw1h41(weapon1)"~
  END

  MAKE_PATCH
    xp_value=>0		// Summoned, already part of vampire XP
    hitpoints=>"hp_max + level1 * 3"
    thac0=>"thac0 - 3"
    attacks=>6
    save_vs_death=>"save_vs_death - 3"
    save_vs_wands=>"save_vs_wands - 3"
    save_vs_polymorph=>"save_vs_polymorph - 3"
    save_vs_breath=>"save_vs_breath - 3"
    save_vs_spells=>"save_vs_spells - 3"
    ac=>"ac_natural - 3"
    replace_items=>"p1-12(weapon1)"
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature		= "wolfdi=>mh#tswlf"
    edits		= "patch_data"
  END

  COPY_EXISTING "bd7000.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "wolfdi" "mh#tswlf"
    END

  LAUNCH_ACTION_FUNCTION extend
    STR_VAR
    script	= "bdtsolak"
    top		= "scripts/spawn_tsolak_shadows"
  END

  LAUNCH_ACTION_FUNCTION extend
    STR_VAR
    script	= "bdcof01"
    top		= "scripts/tsolak_2nd_wind"
  END

  COMPILE "%mod_root%/%component_loc%/scripts/mh#ts2nd.baf"

  MAKE_PATCH
    add_actor_inline'1=>~actor_name=>"{literal:Wight}" actor_resource=>"bdwight" actor_x_coord=>245 actor_y_coord=>150 actor_orientation=>2~
    add_actor_inline'2=>~actor_name=>"{literal:Wight}" actor_resource=>"bdwight" actor_x_coord=>280 actor_y_coord=>200 actor_orientation=>1~
    add_actor_inline'3=>~actor_name=>"{literal:Wight}" actor_resource=>"bdwight" actor_x_coord=>460 actor_y_coord=>125 actor_orientation=>3~
    add_actor_inline'3=>~actor_name=>"{literal:Wight}" actor_resource=>"bdwight" actor_x_coord=>445 actor_y_coord=>200 actor_orientation=>4~
  END

  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area		= "bd0115"
    edits		= "patch_data"
  END
  COPY_EXISTING "bd0115.are" "override"
END	// tsolak_battle


