// Made in Heaven - Encounters & Quests
//
// WeiDU installer

BACKUP "mih_eq/backup"

AUTHOR "angel@pearlgates.net"
VERSION v1

//MODDER missing_eval none missing_resref none
ASK_EVERY_COMPONENT
NO_IF_EVAL_BUG
AUTO_EVAL_STRINGS
AUTO_TRA "mih_eq/language/%s"

ALWAYS
  // Do the following only once per install
  ACTION_IF NOT VARIABLE_IS_SET done_init
  BEGIN
    OUTER_SET done_init = 1


    // We no longer support vanilla BG1 or Tutu, it's too much of a hassle.

    ACTION_IF GAME_IS "bg1 totsc tutu tutu_totsc"
    BEGIN
      FAIL ~Sorry, but this mod no longer supports this version of the game.~
    END

    // Load compatibility variables for various implementations of BG1.

    INCLUDE "%MOD_FOLDER%/lib/g3_bgt_cpmvars.tpa"
    INCLUDE "%MOD_FOLDER%/lib/g3_bgee_cpmvars.tpa"
    INCLUDE "%MOD_FOLDER%/lib/g3_eet_cpmvars.tpa"


    // BG(2)EE compat: handle charsets

    ACTION_DEFINE_ARRAY mh#reload BEGIN "setup-%MOD_FOLDER%" END
    ACTION_DEFINE_ARRAY mh#noconvert BEGIN END

    LAUNCH_ACTION_FUNCTION HANDLE_CHARSETS
      INT_VAR
      infer_charset	= 1
      STR_VAR
      default_language	= "english"
      tra_path		= "%MOD_FOLDER%/language"
      iconv_path	= "%MOD_FOLDER%/bin/win32/iconv"
      noconvert_array	= "mh#noconvert"
      reload_array	= "mh#reload"
    END


    // Load macro/function libraries

    INCLUDE "%MOD_FOLDER%/lib/mh_functions.tph"
    INCLUDE "%MOD_FOLDER%/lib/cd_functions.tph"


    // Read the .ini file for this mod

    LAUNCH_ACTION_MACRO mh_process_ini_file
  END	// done_init block
END	// ALWAYS block


LANGUAGE ~English~
         ~english~
	 ~%MOD_FOLDER%/language/english/setup-%MOD_FOLDER%.tra~


// Enhanced Xvart Village

BEGIN ~Enhanced Xvart Village~
GROUP ~BG1 Encounters~ GAME_INCLUDES "bg1"

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "enhance_xvart_village"
END


// Enhanced Dread Wolves

BEGIN ~Enhanced Dread Wolves~
GROUP ~Enhanced Monsters~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "enhance_dread_wolves"
END


// Enhanced Winter Wolves

BEGIN ~Enhanced Winter Wolves~
GROUP ~Enhanced Monsters~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "enhance_winter_wolves"
END


