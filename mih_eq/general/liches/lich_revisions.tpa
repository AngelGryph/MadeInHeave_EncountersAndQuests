DEFINE_ACTION_FUNCTION lich_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "lich_spell_level_immunity"
    RET
    lich_spell_level_immunity	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "lich_powers_save_penalty"
    RET
    lich_powers_save_penalty	= value
  END


  // Lich Fear

  MAKE_PATCH
    patch_effect_inline=>~match=>"save_vs_death = 1" save_bonus=>"-%lich_powers_save_penalty%"~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "LICH_FEAR"
    edits	= "patch_data"
  END


  // Lich Resistances

  MAKE_PATCH
    delete_effect_global=>~opcode is_in [102 206] and not resource is_in [spwm113 spwi415 spwi711]~
    // 3.5e rule: Skeletal undead resistances
    add_effect_global_inline'1=>~number_to_add=>3 opcode=>"entry_index from [86 88 89]" target=>1 timing=>2 parameter1=>50 parameter2=>1~
    // Turn immunity
    add_effect_global_inline'2=>~opcode=>297 target=>1 timing=>2 parameter2=>1~
    // Immunity to spell levels
    add_effect_global_inline'3=>~number_to_add=>"%lich_spell_level_immunity%" opcode=>102 target=>1 timing=>2 parameter1=>"entry_index + 1"~
    cd_immunity=>~spell_level_1 spell_level_2 spell_level_3 spell_level_4 spell_level_5 spell_level_6 spell_level_7 spell_level_8 spell_level_9~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    INT_VAR
    allow_missing	= 1
    STR_VAR
    item		= "lich mh#elich mh#slich mh#vgoet"
    edits		= "patch_data"
  END


  // Lich Touch

  MAKE_PATCH
    patch_ability_inline=>~ability_dicenum=>0 ability_dicesize=>0 damage_type=>0 to_hit=>4~
    patch_effect_inline=>~match=>"save_vs_death = 1" save_bonus=>"-%lich_powers_save_penalty%"~
    add_effect_inline=>~opcode=>12 target=>2 timing=>1 parameter2=>0x00020000 dicenum=>1 dicesize=>10~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item		= "lich02 lichel01"
    edits		= "patch_data"
  END
END	// lich_revisions


