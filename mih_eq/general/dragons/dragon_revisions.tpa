DEFINE_ACTION_FUNCTION dragon_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "dragon_hp_per_hd"
    RET
    dragon_hp_per_hd	= value
  END


  // Dragon Claws/Bites
  //
  // - All claws and bites are considered at least +4 weapons
  // - All claws and bites have a range of at least 5
  // - The dragon combat modifier (+12) is added as bonus damage

  MAKE_PATCH
    magical=>1
    enchantmentGT=>4
    patch_ability_inline=>~damage_bonus=>12 damage_type=>6 ability_rangeGT=>5~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "dragblaw dragblue dragbrow draggre1 dragred1 dragsha1 dragsil1"
    edits	= "patch_data"
  END

  MAKE_PATCH
    magical=>1
    enchantmentGT=>4
    patch_ability_inline=>~damage_bonus=>12 damage_type=>7 ability_rangeGT=>5~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "dr1-6s"
    edits	= "patch_data"
  END

  MAKE_PATCH
    patch_ability_inline=>~ability_dicesize=>"object_index from [4 8 10]"~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "dr1-6s=>dr1-4s dr1-6s=>dr1-8s dr1-6s=>dr1-10s"
    edits	= "patch_data"
  END


  // Dragon Innates
  //
  // - Dragon stone skin is an innate and always gives the maximum # skins

  MAKE_PATCH
    spell_type=>"innate"
    delete_ability=>~ability_min_level < 20~
    patch_ability_inline=>~ability_min_level=>1 ability_icon_loc=>4 casting_time=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "DRAGON_STONE_SKIN"
    edits	= "patch_data"
  END


  // Dragon Breath
  //
  // - Dragon breath is not magical and bypasses MR and Cloak of Mirroring
  // - Dragon breath bypasses Mirror Image (ToBEx/EE only)
  // - The saving throw has a penalty equal to the combat modifier (-12)

  MAKE_PATCH
    secondary=>0	// Bypasses Cloak of Mirroring, thanks DavidW
    patch_effect_inline'1=>~power=>0 resist_dispel=>0 bypass_mirror_image=>1~
    patch_effect_inline'2=>~match=>"save_vs_breath = 1" save_bonus=>"-12"~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= ~SHADOW_DRAGON_BREATH SILVER_DRAGON_BREATH_COLD SILVER_DRAGON_BREATH_PARALIZATION RED_DRAGON_HIT BLACK_DRAGON_BREATH BLUE_DRAGON_BREATH BROWN_DRAGON_BREATH GREEN_DRAGON_BREATH drgrbrht~
    edits	= "patch_data"
  END


  // Dragon Immunities (dragring)
  //
  // - Dragons see through invisibility
  // - Dragons are immune to blindness, backstabbing and death effects
  // - Dragons are immune to normal missiles (already so in EE)

  MAKE_PATCH
    add_ability_inline'1=>~number_to_add=>2 opcode=>"entry_index from [193 292]" parameter2=>1~
    add_ability_inline'2=>~match=>"not %enhanced_edition%" number_to_add=>6 opcode=>83 parameter2=>"entry_index from [4 9 14 19 26 34]"~
    immunity_to_opcode=>"74 13 55 211 19"	// Blind, Slay, Death, Imprisonment, Mind Flayer
    cd_immunity=>"blindness kill_target slay"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "dragring"
    edits	= "patch_data"
  END


  // Dragon minimum stats
  //
  // - STR=25, DEX=>21, CON=>21, INT=>20, WIS=>21, CHA=>21
  // - at least 12hp per hd
  // - 3 or more attacks per round
  // - -4 to hit with missiles
  // - standard dragon immunities (dragring), remove others
  // - 2-weapon proficiency for effective claw/bite dual wielding

  MAKE_PATCH
    hp_currentGT=>"level1 * %dragon_hp_per_hd%"
    hp_maxGT=>"level1 * %dragon_hp_per_hd%"
    strength=>25
    intelligenceGT=>20
    wisdomGT=>21
    dexterityGT=>21
    constitutionGT=>21
    charismaGT=>21
    attacksGT=>3
    ac_vs_missilesLT=>"-4"
    add_proficiencies=>"2weapon=>3"
    remove_items=>"ring97 dragring helmnoan"
    add_items=>"dragring(ring)"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature	= "bazdra02 bazdra03 dragblac dragblue dragbrow dragred dragshad dragsilv firkra02 fsdragon gorsal hdragred hdragsil ohddixth ohryxtra udsilver"
    edits	= "patch_data"
  END
END	// dragon_revisions


