DEFINE_ACTION_FUNCTION spider_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "spider_hd_bonus"
    RET
    spider_hd_bonus	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "spider_ac_bonus"
    RET
    spider_ac_bonus	= value
  END

  // Statistics blocks

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_gargantuan
  BEGIN
    level1GT=>"8 + %spider_hd_bonus%"
    xp_valueGT=>3000
    hp_maxGT=>"level1 * 7 + 8"
    hp_currentGT=>"level1 * 7 + 8"
    thac0LT=>"19 - level1"	// Two better due to 8+8 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_giant
  BEGIN
    level1GT=>"4 + %spider_hd_bonus%"
    xp_valueGT=>650
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"	// One better due to 4+4 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_huge
  BEGIN
    level1GT=>"2 + %spider_hd_bonus%"
    xp_valueGT=>270
    hp_maxGT=>"level1 * 7 + 2"
    hp_currentGT=>"level1 * 7 + 2"
    thac0LT=>"21 - level1"
    ac_naturalLT=>"6 - %spider_ac_bonus%"
    ac_effectiveLT=>"6 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_phase
  BEGIN
    level1GT=>"5 + %spider_hd_bonus%"
    xp_valueGT=>2000
    hp_maxGT=>"level1 * 7 + 5"
    hp_currentGT=>"level1 * 7 + 5"
    thac0LT=>"20 - level1"	// One better due to 5+5 hd
    ac_naturalLT=>"7 - %spider_ac_bonus%"
    ac_effectiveLT=>"7 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_sword
  BEGIN
    level1GT=>"5 + %spider_hd_bonus%"
    xp_valueGT=>2000
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"
    ac_naturalLT=>"3 - %spider_ac_bonus%"
    ac_effectiveLT=>"3 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_vortex
  BEGIN
    level1GT=>"7 + %spider_hd_bonus%"
    xp_valueGT=>2700
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"	// One better due to 7+4 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spider_wraith
  BEGIN
    level1GT=>"3 + %spider_hd_bonus%"
    xp_valueGT=>1400
    hp_maxGT=>"level1 * 7 + 2"
    hp_currentGT=>"level1 * 7 + 2"
    thac0LT=>"21 - level1"
    ac_naturalLT=>"5 - %spider_ac_bonus%"
    ac_effectiveLT=>"5 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
    general=>undead
    resist_cold=>100
    resist_magic_cold=>100
    replace_items=>"mh#spdwr(weapon1)"
  END


  // Patching starts here

  COPY_EXISTING_REGEXP "^.*\.cre$" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_race
      RET race		= value
    END

    PATCH_IF "%race%" STRING_EQUAL_CASE "spider"
    BEGIN
      PATCH_PRINT "Found spider %SOURCE_RES%..."

      LAUNCH_PATCH_FUNCTION CRE_read_class
        RET class	= value
      END

      PATCH_MATCH "%class%"
      WITH
        "spider_giant" WHEN ("%SOURCE_RES%" STRING_EQUAL_CASE "bdspidga")
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a gargantuan spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_gargantuan"
	  END
	END

        "spider_giant"
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a giant spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_giant"
	  END
	END
	
        "spider_huge"
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a huge spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_huge"
	  END
	END
	
        "spider_phase" WHEN (("%SOURCE_RES%" STRING_EQUAL_CASE "smspid02") OR ("%SOURCE_RES%" STRING_EQUAL_CASE "spidvo01") OR ("%SOURCE_RES%" STRING_EQUAL_CASE "ohbmena7"))
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a vortex spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_vortex"
	  END
	END
	
        "spider_phase"
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a phase spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_phase"
	  END
	END
	
        "spider_sword"
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a sword spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_sword"
	  END
	END
	
	"spider_wraith"
	BEGIN
	  PATCH_PRINT "%SOURCE_RES% appears to be a wraith spider; applying patches."
	  LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext="CRE"
	    edits = "spider_wraith"
	  END
	END

	DEFAULT
	  PATCH_PRINT "%SOURCE_RES% is not any type of spider I know about."
      END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// spider_revisions


