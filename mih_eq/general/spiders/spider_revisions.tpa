DEFINE_ACTION_FUNCTION spider_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "spider_hd_bonus"
    RET
    spider_hd_bonus	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "spider_ac_bonus"
    RET
    spider_ac_bonus	= value
  END

  // Statistics blocks

  ACTION_DEFINE_ASSOCIATIVE_ARRAY gargantuan_spider
  BEGIN
    level1GT=>"8 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 8"
    hp_currentGT=>"level1 * 7 + 8"
    thac0LT=>"19 - level1"	// Two better due to 8+8 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY giant_spider
  BEGIN
    level1GT=>"4 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"	// One better due to 4+4 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY huge_spider
  BEGIN
    level1GT=>"2 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 2"
    hp_currentGT=>"level1 * 7 + 2"
    thac0LT=>"21 - level1"
    ac_naturalLT=>"6 - %spider_ac_bonus%"
    ac_effectiveLT=>"6 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY phase_spider
  BEGIN
    level1GT=>"5 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 5"
    hp_currentGT=>"level1 * 7 + 5"
    thac0LT=>"20 - level1"	// One better due to 5+5 hd
    ac_naturalLT=>"7 - %spider_ac_bonus%"
    ac_effectiveLT=>"7 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY sword_spider
  BEGIN
    level1GT=>"5 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"
    ac_naturalLT=>"3 - %spider_ac_bonus%"
    ac_effectiveLT=>"3 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY vortex_spider
  BEGIN
    level1GT=>"7 + %spider_hd_bonus%"
    enforce_xp=>4
    hp_maxGT=>"level1 * 7 + 4"
    hp_currentGT=>"level1 * 7 + 4"
    thac0LT=>"20 - level1"	// One better due to 7+4 hd
    ac_naturalLT=>"4 - %spider_ac_bonus%"
    ac_effectiveLT=>"4 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY wraith_spider
  BEGIN
    level1GT=>"3 + %spider_hd_bonus%"
    enforce_xp=>6
    hp_maxGT=>"level1 * 7 + 2"
    hp_currentGT=>"level1 * 7 + 2"
    thac0LT=>"21 - level1"
    ac_naturalLT=>"5 - %spider_ac_bonus%"
    ac_effectiveLT=>"5 - %spider_ac_bonus%"
    ac_vs_missiles=>"-2"
    enforce_saves=>at_worst
    general=>undead
    resist_cold=>100
    resist_magic_cold=>100
    replace_items=>"mh#spdwr(weapon1)"
  END


  // Patching starts here

  COPY_EXISTING_REGEXP "^.*\.cre$" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_race
      RET race	= value
    END

    LAUNCH_PATCH_FUNCTION CRE_read_class
      RET class	= value
    END

    PATCH_MATCH "%class%"
    WITH
      "spider_giant" WHEN ("%SOURCE_RES%" STRING_EQUAL_CASE "bdspidga")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a gargantuan spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "gargantuan_spider"
        END
      END

      "spider_giant" WHEN ("%race%" STRING_EQUAL_CASE "spider")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a giant spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "giant_spider"
	END
      END
	
      "spider_huge" WHEN ("%race%" STRING_EQUAL_CASE "spider")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a huge spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "huge_spider"
	END
      END
	
      "spider_phase" WHEN (("%SOURCE_RES%" STRING_EQUAL_CASE "smspid02") OR ("%SOURCE_RES%" STRING_EQUAL_CASE "spidvo01") OR ("%SOURCE_RES%" STRING_EQUAL_CASE "ohbmena7"))
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a vortex spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "vortex_spider"
	END
      END
	
      "spider_phase" WHEN ("%race%" STRING_EQUAL_CASE "spider")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a phase spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "phase_spider"
	END
      END

      "spider_sword" WHEN ("%race%" STRING_EQUAL_CASE "spider")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a sword spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "sword_spider"
	END
      END

      "spider_wraith" WHEN ("%race%" STRING_EQUAL_CASE "spider")
      BEGIN
	PATCH_PRINT "%SOURCE_RES% appears to be a wraith spider; applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "wraith_spider"
	END
      END

      DEFAULT
    END

    BUT_ONLY_IF_IT_CHANGES
END	// spider_revisions


