DEFINE_ACTION_FUNCTION slime_revisions
BEGIN
  // Statistic blocks

  ACTION_DEFINE_ASSOCIATIVE_ARRAY gray_ooze
  BEGIN
    resist_fire=>100
    resist_cold=>100
    resist_magic=>100
    resist_slashingGT=>30
    resist_crushingGT=>30
    resist_piercingGT=>30
    resist_missileGT=>100
    swap_script=>"wtarsght=>wtasight dw1ranmo=>dw1melmo"
    replace_items=>"mh#grooz(weapon1) mh#grooz(weapon2)"
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY mustard_jelly
  BEGIN
    xp_valueGT=>4000
    resist_magicLT=>10
    resist_slashingGT=>30
    resist_crushingGT=>30
    resist_piercingGT=>30
    resist_missile=>100
    hide_in_shadowsGT=>90
    move_silentlyGT=>90
    insert_script_high=>"mh#musjl"
    swap_script=>"wtarsght=>wtasight dw1ranmo=>dw1melmo"
    replace_items=>"mh#musjl(weapon1) mh#musjl(weapon2)"
    add_effect_inline=>"opcode=>83 target=>1 timing=>9 parameter2=>36"
  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY ochre_jelly
  BEGIN
    resist_cold=>0
    resist_magic_cold=>0
    resist_electricity=>100
    resist_magic=>0
    resist_slashingGT=>30
    resist_crushingGT=>30
    resist_piercingGT=>30
    resist_missileGT=>100
    swap_script=>"wtarsght=>wtasight dw1ranmo=>dw1melmo"
    replace_items=>"mh#ochjl(weapon1) mh#ochjl(weapon2)"
  END

  // Patching starts here

  COPY_EXISTING_REGEXP "^.*\.cre$" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_race
      RET
      race	= value
    END

    LAUNCH_PATCH_FUNCTION CRE_read_class
      RET
      class	= value
    END

    PATCH_MATCH "%class%"
    WITH
      "grey_ooze" WHEN ("%race%" STRING_EQUAL_CASE "slime")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a gray ooze, applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "gray_ooze"
	END
      END

      "ochre_jelly" WHEN ("%race%" STRING_EQUAL_CASE "slime")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a ochre jelly, applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "ochre_jelly"
	END
      END

      "mustard_jelly" WHEN ("%race%" STRING_EQUAL_CASE "slime")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a mustard jelly, applying patches."
	LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext="CRE"
	  edits = "mustard_jelly"
	END
      END

      DEFAULT
    END

    BUT_ONLY_IF_IT_CHANGES
END	// slime_revisions


