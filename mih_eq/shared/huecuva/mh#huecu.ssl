BEGIN_ACTION_DEFINITION
Name(CastSpell)
TRIGGER
  HaveSpell(scsargument1)
  !GlobalTimerNotExpired("cast_spell", "locals")
ACTION	
  RESPONSE #scsprob1
    SetGlobalTimer("cast_spell", "locals", ONE_ROUND)
    Spell(scstarget, scsargument1)
END

IF TRIGGER
  Target(Myself)
  !AttackedBy([ANYONE], MELEE)
  ActuallyInCombat()
  TriggerBlock(AoEFriendly)
THEN DO
  Action(CastSpell, CLERIC_DEFENSIVE_HARMONY)
  Action(CastSpell, CLERIC_STRENGTH_OF_ONE)
  Action(CastSpell, CLERIC_BLESS)
END

IF TRIGGER
  Target(Myself)
  !CheckStat(Myself, 1, SANCTUARY)
  ActuallyInCombat()
  !AttackedBy([ANYONE], MELEE)
THEN DO
  Action(CastSpell, CLERIC_SANCTUARY)
END

IF TRIGGER
  Target(Myself)
  ActuallyInCombat()
  !AttackedBy([ANYONE], MELEE)
THEN DO
  Action(CastSpell, CLERIC_RIGHTEOUS_MAGIC)
  Action(CastSpell, CLERIC_DRAW_UPON_HOLY_MIGHT)
  Action(CastSpell, CLERIC_ARMOR_OF_FAITH)
  Action(CastSpell, CLERIC_CLOAK_OF_FEAR)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(DispelTarget)
THEN DO
  Action(CastSpell, CLERIC_DISPEL_MAGIC)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  Range(scstarget, 5)
  Target(Myself)
THEN DO
  Action(CastSpell, CLERIC_SLAY_LIVING)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Helpless|MR)
THEN DO
  Action(CastSpell, CLERIC_HOLD_PERSON)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Helpless|MR|Charm)
THEN DO
  Action(CastSpell, CLERIC_MENTAL_DOMINATION)
  Action(CastSpell, CLERIC_CHARM_PERSON)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(MR)
THEN DO
  Action(CastSpell, CLERIC_FLAME_STRIKE)
  Action(CastSpell, CLERIC_ANIMATE_DEAD)
  Action(CastSpell, CLERIC_UNHOLY_BLIGHT)
  Action(CastSpell, CLERIC_DOOM)
END

IF TRIGGER
  Target(NearestEnemyOf(Myself))
  Range(scstarget, 10)
  !StateCheck(Myself, STATE_DRAWUPONHOLYMIGHT)
THEN DO
  Action(Literal)
  RunAwayFromNoLeaveArea(scstarget, 15)
END


