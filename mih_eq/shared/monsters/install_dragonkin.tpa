DEFINE_ACTION_FUNCTION install_dragonkin
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "use_ee_half_damage"
    RET
    use_ee_half_damage	= value
  END


  // Dracolisk

  MAKE_PATCH
    say_name=>5
    patch_effect_inline=>~match=>"%enhanced_edition% and %use_ee_half_damage% and opcode = 12 and save_vs_spells" dicenum=>4 mode=>256~
    delete_effect=>~%enhanced_edition% and %use_ee_half_damage% and opcode = 12 and not save_vs_spells~
  END

  LAUNCH_ACTION_FUNCTION install_spell
    STR_VAR
    spell	= "dracolisk_breath"
    idsname	= "DRACOLISK_BREATH"
    edits	= "patch_data"
    location	= "spells"
    locbase
  END

  LAUNCH_ACTION_FUNCTION ssl_to_bcs
    STR_VAR
    script	= "mh#draco"
    location	= "scripts"
    locbase
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "basill2=>mh#draco"
    editstring	= ~patch_ability_inline=>"ability_dicenum=>3 ability_dicesize=>4"~
  END

  MAKE_PATCH
    level1=>8
    enforce_saves=>at_worst
    xp_value=>3000
    hitpoints=>52
    thac0=>13
    attacks=>3
    ac=>3
    resist_acid=>100
    alignment=>"chaotic_evil"
    add_effect_inline=>"opcode=>52 target=>1 timing=>9 parameter1=>0x03020800 parameter2=>2"
    insert_script=>"mh#draco"
    add_items=>"dragring(lring)"
    replace_items=>"mh#draco(weapon2)"
    say_both_names=>4
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature		= "basill=>mh#draco basles01=>mh#draco"
    edits		= "patch_data"
  END
END	// install_dragonkin


