DEFINE_ACTION_FUNCTION setup_barrow_wight
BEGIN
  ACTION_IF NOT FILE_EXISTS_IN_GAME "mh#bwght"
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "e25e.ini" AND NOT GAME_INCLUDES "sod"
    BEGIN
      // Non-SoD EE games ship without the barrow wight animation and sounds
      // even though the animate.ids entries and e25e.ini file are there.

      LAUNCH_ACTION_FUNCTION install
        STR_VAR
	file		= "all"
	postfix		= "bam"
        locbase
      END

      LAUNCH_ACTION_FUNCTION install
        STR_VAR
	file		= "all"
	postfix		= "wav"
        locbase
      END
    END

    LAUNCH_ACTION_FUNCTION clone_item
      STR_VAR
      item		= "wight=>mh#bwght"
      editstring	= ~magical=>1 enchantment=>1 patch_ability_inline=>"ability_numdice=>3 ability_dicesize=>4"~
    END

    ACTION_IF IDS_OF_SYMBOL("animate" "wight_barrow") != "-1"
    BEGIN
      OUTER_TEXT_SPRINT editstring	~say_both_names=>0 animation=>"wight_barrow"~
    END
    ELSE
    BEGIN
      OUTER_TEXT_SPRINT editstring	~say_both_names=>0~
    END

    LAUNCH_ACTION_FUNCTION install_creature
      STR_VAR
      creature		= "mh#bwght"
      editstring
      locbase
    END
  END
END	// setup_barrow_wight


