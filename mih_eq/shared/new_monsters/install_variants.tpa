DEFINE_ACTION_FUNCTION install_variants
BEGIN
  // Golden Bones (15 hd Skeleton Warrior)

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature		= "skelwa=>mh#skw15"
    editstring		= ~level1=>15 strength=>20 hitpoints=>140 ac=>"-5" attacks=>3 enforce_thac0=>at_worst enforce_saves=>at_worst enforce_xp=>5 replace_items=>"sw2h11(weapon1)" animation=>"skeleton" color_skin=>67 say_both_names=>7~
  END


  // Greater Zombie (12 hd Zombie)

  LAUNCH_ACTION_FUNCTION clone_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature		= "zombie01=>mh#zom12 zombie=>mh#zom12"
    editstring		= ~level1=>12 hitpoints=>80 ac=>0 attacks=>2 enforce_thac0=>at_worst enforce_saves=>at_worst enforce_xp=>1 resist_crushing=>50 resist_missile=>85 replace_items=>"b1-8m2(weapon1) immune2(rring)" say_both_names=>8~
  END


  // Water Elementals (8/12 hd elementals)

  ACTION_IF NOT FILE_EXISTS_IN_GAME "elwatg01.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "bdwelems=>elwatl01 bdweleds=>elwat01 bdweledg=>elwatg01"
      editstring	= ~animation=>water_weird script_class=>none dv=>DEST_RES~
    END
  END
  ELSE
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "elwatg01=>elwatl01 elwatg01=>elwat01"
      editstring	= ~level1=>"object_index from [8 12]" enforce_hp=>at_best enforce_thac0=>at_best enforce_saves=>at_best enforce_xp=>4 say_both_names=>"object_index from [13 14]"~
    END
  END


  // Elite Orcs (12 hd orcs)

  LAUNCH_ACTION_FUNCTION clone_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature		= "orc01=>mh#orc12 bdorc1d=>mh#orc12 orc02=>mh#ora12 bdorc2d=>mh#ora12"
    editstring		= ~strength=>18 strength_ex=>51 dexterity=>18 level1=>12 enforce_hp=>at_worst enforce_thac0=>at_worst enforce_saves=>at_worst enforce_xp=>4 say_both_names=>"object_index from [15 15 16 16]"~
  END
END	// install_variants


