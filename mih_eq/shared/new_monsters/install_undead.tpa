DEFINE_ACTION_FUNCTION install_undead
BEGIN
  // Wights (Wight, Greater Wight, Barrow Wight)

  ACTION_IF FILE_EXISTS_IN_GAME "e25e.ini" AND NOT GAME_INCLUDES "sod"
  BEGIN
    // Non-SoD EE games ship without the barrow wight animation and sounds
    // even though the animate.ids entries and e25e.ini file are there.

    LAUNCH_ACTION_FUNCTION install
      STR_VAR
      file	= "all"
      postfix	= "bam"
      location	= "graphics"
      locbase
    END

    LAUNCH_ACTION_FUNCTION install
      STR_VAR
      file	= "all"
      postfix	= "wav"
      location	= "sounds"
      locbase
    END
  END

  MAKE_PATCH
    magical=>1
    enchantment=>"object_index from [1 2]"
    patch_ability_inline=>~ability_numdice=>"object_index from [1 3]" ability_dicesize=>4~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "wight=>mh#wight wight=>mh#bwght"
    edits	= "patch_data"
  END

  ACTION_IF IDS_OF_SYMBOL("animate" "wight_gray") != "-1"
  BEGIN
    OUTER_TEXT_SPRINT editstring ~say_both_names=>"object_index from [19 19 19 20]" animation=>"wight_gray"~
  END
  ELSE
  BEGIN
    OUTER_TEXT_SPRINT editstring ~say_both_names=>"object_index from [19 19 19 20]"~
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#wight mh#wght5 mh#wght6 mh#wght7"
    editstring
    location	= "actors"
    locbase
  END

  ACTION_IF IDS_OF_SYMBOL("animate" "wight_barrow") != "-1"
  BEGIN
    OUTER_TEXT_SPRINT editstring ~say_both_names=>0 animation=>"wight_barrow"~
  END
  ELSE
  BEGIN
    OUTER_TEXT_SPRINT editstring ~say_both_names=>0~
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#bwght"
    editstring
    location	= "actors"
    locbase
  END


  // Blazing Bones

  LAUNCH_ACTION_FUNCTION install_spell
    STR_VAR
    spell	= "bone_blaze"
    idsname	= "BONE_BLAZE"
    location	= "spells"
    locbase
    editstring	= "say_name=>2"
  END

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "mh#blaze.baf"
    location	= "scripts"
    locbase
  END

  MAKE_PATCH
    delete_opcodes=>"1"
    patch_ability_inline=>~to_hit=>0 ability_numdice=>0 ability_dicesize=>0 damage_bonus=>0 damage_type=>4 ability_range=>20 number_charges=>0 when_drained=>0~
    patch_effect_inline=>~match=>"opcode=12" dicenum=>1 dicesize=>6 power=>0~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "melfmet=>mh#blzrg"
    edits	= "patch_data"
  END

  MAKE_PATCH
    add_effect_inline=>~opcode=>12 target=>2 timing=>1 parameter1=>2 parameter2=>524288~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "melfmet=>mh#blzml"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#blaze"
    location	= "actors"
    locbase
    editstring	= "say_both_names=>1"
  END


  // Huecuva

  LAUNCH_ACTION_FUNCTION ssl_to_bcs
    STR_VAR
    script	= "mh#huecu"
    location	= "scripts"
    locbase
    ssllibrary	= "%mod_root%/ssl/library"
  END

  MAKE_PATCH
    magical=>1
    enchantment=>1
    add_effect_inline'1=>"opcode=>78 target=>2 timing=>1 parameter1=>2 parameter2=>4 save_vs_poison=>1"
    add_effect_inline'2=>"opcode=>78 target=>2 timing=>1 parameter1=>2 parameter2=>6 save_vs_poison=>1"
    add_effect_inline'3=>"opcode=>142 target=>2 timing=>1 parameter2=>7 save_vs_poison=>1"
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "s1-6=>mh#huecu"
    edits	= "patch_data"
  END

  MAKE_PATCH
    level1=>5
    ac=>3
    xp_value=>650
    enforce_thac0=>at_worst
    enforce_saves=>at_worst
    enforce_hp=>at_worst
    say_both_names=>9
    add_all_priest_spells=>null
    add_spells=>"CLERIC_BLESS CLERIC_SANCTUARY CLERIC_DOOM CLERIC_HOLD_PERSON CLERIC_CHARM_PERSON CLERIC_DRAW_UPON_HOLY_MIGHT CLERIC_ANIMATE_DEAD CLERIC_STRENGTH_OF_ONE"
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "huecuva=>mh#huec5"
    edits	= "patch_data"
    location	= "actors"
    locbase
  END

  MAKE_PATCH
    level1=>9
    ac=>1
    xp_value=>2000
    enforce_thac0=>at_worst
    enforce_saves=>at_worst
    enforce_hp=>at_worst
    say_both_names=>9
    add_all_priest_spells=>null
    add_spells=>"CLERIC_BLESS CLERIC_SANCTUARY CLERIC_DOOM CLERIC_ARMOR_OF_FAITH CLERIC_HOLD_PERSON CLERIC_CHARM_PERSON(2) CLERIC_DRAW_UPON_HOLY_MIGHT CLERIC_ANIMATE_DEAD CLERIC_UNHOLY_BLIGHT CLERIC_STRENGTH_OF_ONE CLERIC_DEFENSIVE_HARMONY CLERIC_CLOAK_OF_FEAR CLERIC_MENTAL_DOMINATION CLERIC_FLAME_STRIKE CLERIC_RIGHTEOUS_MAGIC"
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "huecuva=>mh#huec9"
    edits	= "patch_data"
    location	= "actors"
    locbase
  END


  // Mohrg

  MAKE_PATCH
    patch_ability_inline=>"ability_numdice=>2 ability_dicesize=>6"
    patch_effect_inline=>"save_bonus=>-4"
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "carrio1=>mh#mohrg"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#mohrg"
    location	= "actors"
    locbase
    editstring	= "say_both_names=>10"
  END


  // Nightwalker

  MAKE_PATCH
    magical=>1
    enchantment=>3
    add_effect_inline'1=>"opcode=>243 target=>2 parameter1=>1"
    add_effect_inline'2=>"opcode=>244 target=>2 parameter1=>1"
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "b2-16=>mh#ngtwk"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "mh#ngtwk.baf"
    location	= "scripts"
    locbase
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#ngtwk"
    location	= "actors"
    locbase
    editstring	= "say_both_names=>18"
  END
END	// install_undead


