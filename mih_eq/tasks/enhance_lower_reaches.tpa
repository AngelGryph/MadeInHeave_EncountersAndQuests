// Rift Device Cavern

COPY_EXISTING "ar0204.are" "override"
  // Add new actors
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/lower_reaches.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0076) 200
    WRITE_SHORT (spawn_offset + 0x0084) 6

    WRITE_SHORT (spawn_offset + 0x0074) 10

    WRITE_ASCII (spawn_offset + 0x0024) "RDUndead" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDUndea2" #8
    WRITE_ASCII (spawn_offset + 0x0034) "RDShad" #8
    WRITE_ASCII (spawn_offset + 0x003c) "RDSpider" #8
    WRITE_ASCII (spawn_offset + 0x0044) "RDSpid2" #8
    WRITE_ASCII (spawn_offset + 0x004c) "RDSpid3" #8
    WRITE_ASCII (spawn_offset + 0x0054) "RDSlim1" #8
    WRITE_ASCII (spawn_offset + 0x005c) "RDSlim2" #8
    WRITE_ASCII (spawn_offset + 0x0064) "RDMist1" #8
    WRITE_ASCII (spawn_offset + 0x006c) "RDMist2" #8
  END

  // High level trap spawns undead instead of beholders
  // (Makes more sense, the Unseeing Eye wants the Rift Device found!)
  LAUNCH_PATCH_FUNCTION ALTER_AREA_REGION
    STR_VAR
    region_name		= "Chapter 6 trap"
    region_script	= "spwndead"
  END


// Hand Trap spawns wraith spiders instead of huge ones

COPY_EXISTING "handtrp.bcs" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "spidhu01" "spidwr01"
  END


// Undead City

COPY_EXISTING "ar0206.are" "override"
  // Add new actors
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/undead_city.2da"
  END


