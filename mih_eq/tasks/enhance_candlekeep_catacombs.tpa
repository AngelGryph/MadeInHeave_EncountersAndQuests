// Enhanced Candlekeep Catacombs


// More dopplegangers

COMPILE "%MOD_FOLDER%/dialog/dopplegangers.d"


COPY "%MOD_FOLDER%/actors/mh#erik3.cre" "override"
  SAY NAME1 "Erik"
  SAY NAME2 "Erik"


COPY "%MOD_FOLDER%/actors/mh#doppr.cre" "override"
  SAY NAME1 "Priest of Oghma"
  SAY NAME2 "Priest of Oghma"


COPY "%MOD_FOLDER%/actors/mh#dopkp.cre" "override"
  SAY NAME1 "Keeper of the Portal"
  SAY NAME2 "Keeper of the Portal"


// Area Edits

COPY_EXISTING "%Candlekeep_Catacombs_L1%.are" "override"
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/candlekeep_catacombs_l1.2da"
  END


COPY_EXISTING "%Candlekeep_Catacombs_L2%.are" "override"
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/candlekeep_catacombs_l2.2da"
  END


COPY_EXISTING "%CandlekeepCaves%.are" "override"
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/candlekeep_caves.2da"
  END


// Improved Arkanis and Deder

COPY_EXISTING "arkani3.cre" "override"
  // Equipment
  REPLACE_CRE_ITEM "ax1h02" #0 #0 #0 "none" "weapon1" EQUIP
  REPLACE_CRE_ITEM "hamm02" #0 #0 #0 "none" "weapon2"
  REPLACE_CRE_ITEM "shld06" #0 #0 #0 "none" "shield" EQUIP
  REPLACE_CRE_ITEM "chan02" #0 #0 #0 "none" "armor"
  REPLACE_CRE_ITEM "helm08" #0 #0 #0 "none" "helmet"
  REPLACE_CRE_ITEM "potn08" #5 #0 #0 "none" "qitem1"
  REPLACE_CRE_ITEM "potn09" #1 #0 #0 "none" "qitem2"
  REPLACE_CRE_ITEM "potn12" #1 #0 #0 "none" "qitem3"
  REMOVE_CRE_ITEM "iarow01"

  // Proficiencies
  WRITE_BYTE	0x0074	0	// Axe prof.
  WRITE_BYTE	0x0075	0	// Missile prof.
  SET_BG2_PROFICIENCY	PROFICIENCYAXE			3
  SET_BG2_PROFICIENCY	PROFICIENCYWARHAMMER		2
  SET_BG2_PROFICIENCY	PROFICIENCYSWORDANDSHIELD	2

  // Stats
  WRITE_LONG	0x0014	975	// XP value
  WRITE_SHORT	0x0024	56	// Current HP
  WRITE_SHORT	0x0026	56	// Maximum HP
  WRITE_BYTE	0x0053	7	// # of attacks 3/2
  WRITE_BYTE	0x0054	8	// Save vs. Death
  WRITE_BYTE	0x0055	10	// Save vs. Wands
  WRITE_BYTE	0x0056	9	// Save vs. Poly
  WRITE_BYTE	0x0057	9	// Save vs. Breath
  WRITE_BYTE	0x0058	11	// Save vs. Spell
  WRITE_BYTE	0x0234	9	// Level
  WRITE_BYTE	0x0273	IDS_OF_SYMBOL("class" "fighter")


COPY_EXISTING "deder3.cre" "override"
  // Equipment
  REPLACE_CRE_ITEM "bow06" #0 #0 #0 "none" "weapon1" EQUIP
  REPLACE_CRE_ITEM "sw1h08" #0 #0 #0 "none" "weapon2"
  REPLACE_CRE_ITEM "arow02" #20 #0 #0 "none" "quiver1"
  REPLACE_CRE_ITEM "arow02" #20 #0 #0 "none" "quiver2"
  REPLACE_CRE_ITEM "arow02" #20 #0 #0 "none" "quiver3"
  REPLACE_CRE_ITEM "leat05" #0 #0 #0 "none" "armor"
  REPLACE_CRE_ITEM "potn08" #5 #0 #0 "none" "qitem1"
  REPLACE_CRE_ITEM "potn10" #2 #0 #0 "none" "qitem2"
  REPLACE_CRE_ITEM "potn13" #1 #0 #0 "none" "qitem3"

  // Proficiencies
  WRITE_BYTE	0x006f	0	// Small sword prof.
  WRITE_BYTE	0x0075	0	// Missile prof.
  SET_BG2_PROFICIENCY	PROFICIENCYSHORTSWORD		2
  SET_BG2_PROFICIENCY	PROFICIENCYSHORTBOW		2
  SET_BG2_PROFICIENCY	PROFICIENCYSINGLEWEAPON		2

  // Stats
  WRITE_LONG	0x0014	975	// XP value
  WRITE_SHORT	0x0024	42	// Current HP
  WRITE_SHORT	0x0026	42	// Maximum HP
  WRITE_BYTE	0x0053	7	// # of attacks 3/2
  WRITE_BYTE	0x0054	10	// Save vs. Death
  WRITE_BYTE	0x0055	12	// Save vs. Wands
  WRITE_BYTE	0x0056	11	// Save vs. Poly
  WRITE_BYTE	0x0057	12	// Save vs. Breath
  WRITE_BYTE	0x0058	13	// Save vs. Spell
  WRITE_BYTE	0x0065	50	// Set traps
  WRITE_BYTE	0x0068	70	// Stealth
  WRITE_BYTE	0x0069	70	// Find/disarm traps
  WRITE_BYTE	0x0234	7	// Level (fighter)
  WRITE_BYTE	0x0235	9	// Level (thief)
  WRITE_BYTE	0x0273	IDS_OF_SYMBOL("class" "fighter_thief")

  ADD_MEMORIZED_SPELL "spcl412" #0 "innate" (2)


<<<<<<<< inline/arkanis_ally.d
ADD_TRANS_ACTION "arkani3"
BEGIN 1 END	// States
BEGIN END	// Transaction
~ActionOverride("deder", Ally())
Ally()~
>>>>>>>>

COMPILE "inline/arkanis_ally.d" USING "%MOD_FOLDER%/dummy.tra"


// Oobah & Blahr encounter

COMPILE "%MOD_FOLDER%/dialog/oobah_blahr.d"


LAUNCH_ACTION_FUNCTION mh_batch_install_files
  STR_VAR
  dir_path	= "%MOD_FOLDER%/portraits"
  file_ext	= "bmp"
END


COPY "%MOD_FOLDER%/actors/mh#oobah.cre" "override"
  SAY NAME1 ~Oobah~
  SAY NAME2 ~Oobah~


COPY "%MOD_FOLDER%/actors/mh#blahr.cre" "override"
  SAY NAME1 ~Blahr~
  SAY NAME2 ~Blahr~


EXTEND_BOTTOM "%Candlekeep_Ch6_BCS%.bcs" "%MOD_FOLDER%/scripts/spawn_oogah_blahr.baf" USING "%MOD_FOLDER%/dummy.tra"


