// Enhanced Zombie Farm + Hag Quest

ACTION_IF NOT FILE_EXISTS_IN_GAME "b3-12m1.itm"
BEGIN
  COPY_EXISTING "b1-4.itm" "override/b3-12m1.itm"
    LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
      INT_VAR
      dicenumber	= 3
    END

    LAUNCH_PATCH_FUNCTION mh_make_magical
      INT_VAR
      enchantment	= 1
    END
END


COPY_EXISTING "zombiew.cre" "override"
  WRITE_LONG 0x0014 975		// XP value
  WRITE_SHORT 0x0024 36		// current HP
  WRITE_SHORT 0x0026 36		// maximum HP
  WRITE_SHORT 0x0046 6		// base AC
  WRITE_SHORT 0x0048 6		// eff. AC
  WRITE_BYTE 0x0052 15		// THAC0
  WRITE_BYTE 0x0054 13		// save vs. death
  WRITE_BYTE 0x0055 15		// save vs. wand
  WRITE_BYTE 0x0056 14		// save vs. poly
  WRITE_BYTE 0x0057 16		// save vs. breath
  WRITE_BYTE 0x0058 16		// save vs. spell
  WRITE_BYTE 0x0059 50		// resist fire
  WRITE_BYTE 0x005a 100		// resist cold
  WRITE_BYTE 0x005b 100		// resist elec.
  WRITE_BYTE 0x0061 50		// resist crushing
  WRITE_BYTE 0x0062 50		// resist piercing
  WRITE_BYTE 0x0063 90		// resist missile

  WRITE_BYTE 0x0234 4		// level/HD
  WRITE_BYTE 0x0238 18		// Strength
  WRITE_BYTE 0x0239 10		// Strength EX
  WRITE_BYTE 0x023c 19		// Dexterity
  WRITE_BYTE 0x027b IDS_OF_SYMBOL("alignmen" "neutral_evil")

  ADD_CRE_ITEM "immune1" #0 #0 #0 "unstealable&undroppable" "rring"
  REPLACE_CRE_ITEM "b3-12m1" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP
  REPLACE_CRE_ITEM "b3-12m1" #0 #0 #0 "unstealable&undroppable" "weapon2"
  REPLACE_CRE_ITEM "b3-12m1" #0 #0 #0 "unstealable&undroppable" "weapon3"
  REPLACE_CRE_ITEM "b3-12m1" #0 #0 #0 "unstealable&undroppable" "weapon4"

  LAUNCH_PATCH_FUNCTION ADD_CRE_EFFECT
    INT_VAR
    opcode	= 83	// Protection: From Projectile
    target	= 1	// Self
    timing	= 1	// Permanent (till death)
    parameter2	= IDS_OF_SYMBOL("projectl" "magicmis")
  END


COMPILE "%MOD_FOLDER%/dialog/zombie_farm.d" USING "%MOD_FOLDER%/dummy.tra"


