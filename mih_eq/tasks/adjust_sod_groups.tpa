COPY_EXISTING "bdlnorm.bcs" "override"
              "bdlcore.bcs" "override"
              "bdlhard.bcs" "override"
              "bdlinsa.bcs" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    PATCH_IF group_size > 0
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        "DifficultyLT(EASY)" "False()"
    END

    PATCH_IF group_size > 1
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        "DifficultyLT(NORMAL)" "False()"
    END

    PATCH_IF group_size > 2
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        "DifficultyLT(HARD)" "False()"
    END

    PATCH_IF group_size > 3
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        "DifficultyLT(HARDEST)" "False()"
    END
  END


// Prevent mummy in dwarven tunnels from killing itself

COPY_EXISTING "bd1100.are" "override"
  LAUNCH_PATCH_FUNCTION ALTER_AREA_ACTOR
    STR_VAR
    actor_name		= "mummy"
    script_override	= "none"
  END

  BUT_ONLY_IF_IT_CHANGES


// Prevent greater shadow in lich outpost from killing itself

COPY_EXISTING "bd1200.are" "override"
  LAUNCH_PATCH_FUNCTION ALTER_AREA_ACTOR
    STR_VAR
    actor_name		= "greater_shadow"
    script_override	= "none"
  END

  BUT_ONLY_IF_IT_CHANGES


