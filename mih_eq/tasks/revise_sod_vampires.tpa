// Revised SoD Vampire Encounters


// ========================
// Revised Tsolak encounter
// ========================

COPY_EXISTING "bdtsolak.cre" "override"
  WRITE_LONG 0x0014 12500	// XP value

  WRITE_SHORT 0x0024 89		// current HP
  WRITE_SHORT 0x0026 89		// maximum HP

  WRITE_SHORT 0x0046 "-1"	// base AC
  WRITE_SHORT 0x0048 "-1"	// eff. AC

  WRITE_BYTE 0x0052 8		// THAC0
  WRITE_BYTE 0x0053 2		// APR
  WRITE_BYTE 0x0054 7		// save vs. death
  WRITE_BYTE 0x0055 9		// save vs. wand
  WRITE_BYTE 0x0056 8		// save vs. poly
  WRITE_BYTE 0x0057 8		// save vs. breath
  WRITE_BYTE 0x0058 10		// save vs. spell
  WRITE_BYTE 0x0059 50		// resist fire
  WRITE_BYTE 0x005c 50		// resist acid
  WRITE_BYTE 0x005d 20		// resist magic
  WRITE_BYTE 0x005e 50		// resist magic fire

  WRITE_BYTE 0x0234 11		// level/HD

  WRITE_BYTE 0x0239 100		// Strength EX
  WRITE_BYTE 0x023a 18		// Intelligence
  WRITE_BYTE 0x023c 18		// Dexterity
  WRITE_BYTE 0x023d 18		// Constitution
  WRITE_BYTE 0x023e 17		// Charisma
  
  LAUNCH_PATCH_FUNCTION ADD_CRE_EFFECT
    INT_VAR
    opcode	= 31	// Magic Damage Resistance
    target	= 1	// Self
    timing	= 1	// Permanent
    parameter1	= 50
    parameter2	= 1
  END

  REPLACE_CRE_ITEM "immune2" #0 #0 #0 "unstealable&undroppable" "rring"


COPY_EXISTING "bdisabel.cre" "override"
  REPLACE_CRE_ITEM "sw1h41" #0 #0 #0 "none" "weapon1" EQUIP


COPY_EXISTING "wolfdi.cre" "override/mh#tswlf.cre"
  LAUNCH_PATCH_FUNCTION mh_easy_alter_monster
    INT_VAR
    modifier	= 3
  END

  WRITE_LONG 0x0014 300		// XP value
  WRITE_BYTE 0x0053 6		// APR

  REPLACE_CRE_ITEM "p1-12" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP
  

COPY_EXISTING "bd7000.bcs" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "wolfdi" "mh#tswlf"
  END


EXTEND_TOP "bdtsolak.bcs" "%MOD_FOLDER%/scripts/spawn_tsolak_shadows.baf" USING "%MOD_FOLDER%/dummy.tra"


COPY_EXISTING "bd0115.are" "override"
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/tsolaks_tomb.2da"
  END


COMPILE "%MOD_FOLDER%/scripts/mh#ts2nd.baf" USING "%MOD_FOLDER%/dummy.tra"


COPY_EXISTING "bdtsolak.cre" "override/mh#ts2nd.cre"
  WRITE_ASCII SCRIPT_OVERRIDE "mh#ts2nd" #8
  WRITE_ASCII DIALOG "none" #8

  WRITE_BYTE 0x0270 IDS_OF_SYMBOL("ea" "enemy")
  

EXTEND_TOP "bdcof01.bcs" "%MOD_FOLDER%/scripts/tsolak_2nd_wind.baf" USING "%MOD_FOLDER%/dummy.tra"


