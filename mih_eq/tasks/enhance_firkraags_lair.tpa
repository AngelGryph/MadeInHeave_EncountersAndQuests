// Enhanced Firkraag's Lair


APPEND_COL "spawngrp.2da" "$ $ RDMHBasl 60 basles01 basles01 basgrt01 * * * * *" UNLESS "RDMHBasl"
APPEND_COL "spawngrp.2da" "$ $ RDMHAnkh 60 ankheg01 ankheg01 ankheg01 ankheg01 ankheg01 * * *" UNLESS "RDMHAnkh"


// Windspear Hills

ADD_JOURNAL @5 @8 @14 @16 USING "%MOD_FOLDER%/language/%LANGUAGE%/windspear_hills.tra"

COMPILE "%MOD_FOLDER%/dialog/windspear_hills.d"


COPY "%MOD_FOLDER%/actors/mh#flpdw.cre" "override"
  SAY NAME1 "Brundul"
  SAY NAME2 "Brundul"


COPY "%MOD_FOLDER%/actors/mh#flpel.cre" "override"
  SAY NAME1 "Ailluin"
  SAY NAME2 "Ailluin"


COPY_EXISTING "ar1200.are" "override"
  LAUNCH_PATCH_FUNCTION mh_add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/tables/windspear_hills.2da"
  END

  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0076) 200
    WRITE_SHORT (spawn_offset + 0x0084) 4

    WRITE_SHORT (spawn_offset + 0x0074) 10

    WRITE_ASCII (spawn_offset + 0x0024) "RDMHBasl" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDMHAnkh" #8
    WRITE_ASCII (spawn_offset + 0x0034) "RDGnoll" #8
    WRITE_ASCII (spawn_offset + 0x003c) "RDOrc" #8
    WRITE_ASCII (spawn_offset + 0x0044) "RDOrog" #8
    WRITE_ASCII (spawn_offset + 0x004c) "RDOgre" #8
    WRITE_ASCII (spawn_offset + 0x0054) "RDHobgob" #8
    WRITE_ASCII (spawn_offset + 0x005c) "RDLiz" #8
    WRITE_ASCII (spawn_offset + 0x0064) "bearca01" #8
    WRITE_ASCII (spawn_offset + 0x006c) "bearpo01" #8
  END


// Mist Cavern

COPY_EXISTING "ar1201.are" "override"
  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0076) 200
    WRITE_SHORT (spawn_offset + 0x0084) 3

    WRITE_SHORT (spawn_offset + 0x0074) 2

    WRITE_ASCII (spawn_offset + 0x0024) "RDMist1" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDMist2" #8
  END


// Main Complex

COPY_EXISTING "ar1202.are" "override"
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0084) (THIS + 3)
  END

  // Add new high level trap
  LAUNCH_PATCH_FUNCTION ADD_AREA_REGION_TRIGGER
    INT_VAR
    ab_RT_BbLX		= 3258
    ab_RT_BbLY		= 461
    ab_RT_BbHX		= 3382
    ab_RT_BbHY		= 545
    ab_RT_VxPr		= 4
    ab_RT_Vx_X_0	= 3382
    ab_RT_Vx_Y_0	= 525
    ab_RT_Vx_X_1	= 3352
    ab_RT_Vx_Y_1	= 545
    ab_RT_Vx_X_2	= 3258
    ab_RT_Vx_Y_2	= 480
    ab_RT_Vx_X_3	= 3285
    ab_RT_Vx_Y_3	= 461
    ab_RT_TDtD		= 100
    ab_RT_TRmD		= 100
    ab_RT_TSet		= 1
    ab_RT_LPoX		= 3245
    ab_RT_LPoY		= 175

    STR_VAR
    ab_RT_Name		= "MiH#HighLevel3"
    ab_RT_Rbcs		= "spwndead"
  END


